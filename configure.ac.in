dnl ***************************
dnl *** Version information ***
dnl ***************************
m4_define([xfce4_dict_version_major], [0])
m4_define([xfce4_dict_version_minor], [7])
m4_define([xfce4_dict_version_micro], [0])
m4_define([xfce4_dict_version_build], [r@REVISION@])
m4_define([xfce4_dict_version_tag], [])
m4_define([xfce4_dict_version], [xfce4_dict_version_major().xfce4_dict_version_minor().xfce4_dict_version_micro()ifelse(xfce4_dict_version_tag(), [git], [xfce4_dict_version_tag()-xfce4_dict_version_build()], [xfce4_dict_version_tag()])])

dnl ***************************
dnl *** Initialize autoconf ***
dnl ***************************
AC_COPYRIGHT([Copyright (c) 2006-2012 Enrico Tr√∂ger. All rights reserved.])
AC_INIT([xfce4-dict-plugin], [xfce4_dict_version], [http://bugzilla.xfce.org/], [xfce4-dict-plugin])
AC_PREREQ([2.50])

dnl ***************************
dnl *** Initialize automake ***
dnl ***************************
AM_INIT_AUTOMAKE([1.8 dist-bzip2 tar-ustar])
AC_CONFIG_HEADERS([config.h])
AM_MAINTAINER_MODE()
m4_ifdef([AM_SILENT_RULES], [AM_SILENT_RULES([yes])])

dnl ********************************
dnl *** Check for basic programs ***
dnl ********************************
AC_PROG_CC()
AM_PROG_CC_C_O
AC_PROG_LD()
AC_PROG_INSTALL()
AC_PROG_INTLTOOL()

dnl ***************************
dnl *** Disable static libs ***
dnl ***************************
LT_PREREQ([2.2.6])
LT_INIT([disable-static])

dnl **********************************
dnl *** Check for standard headers ***
dnl **********************************
AC_HEADER_STDC()
AC_SEARCH_LIBS(floor, m)

dnl ******************************
dnl *** Check for i18n support ***
dnl ******************************
XDT_I18N([@LINGUAS@])

dnl ***********************************
dnl *** Check for required packages ***
dnl ***********************************
XDT_CHECK_PACKAGE([GTHREAD], [gthread-2.0], [2.24.0])
XDT_CHECK_PACKAGE([GTK], [gtk+-2.0], [2.20.0])
XDT_CHECK_PACKAGE([LIBXFCE4UI], [libxfce4ui-1], [4.10.0])
XDT_CHECK_PACKAGE([LIBXFCE4UTIL], [libxfce4util-1.0], [4.10.0])
XDT_CHECK_PACKAGE([LIBXFCE4PANEL], [libxfce4panel-1.0], [4.10.0])
XDT_CHECK_PACKAGE([X11], [x11])

dnl ***********************************
dnl *** Check for debugging support ***
dnl ***********************************
XDT_FEATURE_DEBUG()


AC_OUTPUT([
	Makefile
	lib/Makefile
	panel-plugin/Makefile
	src/Makefile
	po/Makefile.in
])
